---
date created: 2021-12-13
---

# 实验五 8255 并行接口实验

## 1. 实验目的

1. 学习并掌握 8255 的工作方式及其应用。 
2. 掌握 8255 典型应用电路的接法。 
3. 掌握程序固化及脱机运行程序的方法。

## 2. 实验原理

8255 实验单元电路图

![[微机原理/实验/src/Pasted image 20211212172604.png]]

## 3. 实验内容

1. 基本输入输出实验。编写程序，使 8255 的 A 口为输出，B 口为输入，完成拨动开关到数据灯的数据传输。要求只要开关拨动，数据灯的显示就发生相应改变。
2. 流水灯显示实验。编写程序，使 8255 的 A 口和 B 口均为输出，数据灯 D7～D0 由左向右，每次仅亮一个灯，循环显示，D15～D8 与 D7～D0 正相反，由右向左，每次仅点亮一个灯，循环显示。
3. 方式 1 输入输出实验。编写程序，使 8255 工作在方式 1 控制下的 A 口输入，B 口输出。

## 4. 实验 5-1 基本输入输出实验

### 4.1. 实验要求

本实验使 8255 端口 A 工作在**方式 0** 并作为**输出口**，端口 B 工作在**方式 0** 并作为**输入口**。用一组开关信号接入端口 B，端口 A 输出线接至一组数据灯上，然后通过对 8255 芯片编程来实现输入输出功能。

| 方式 |   A组工作方式    |
|:----:|:----------------:|
|  0   | **基本输入输出** | 
|  1   |   选通输入输出   |
|  2   | 双向选通输入输出 |

![[微机原理/实验/src/Pasted image 20211212173211.png]]

| D7  | D6  | D5  | D4  | D3  | D2  | D1  | D0  | 控制字 |
|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:------:|
|  1  |  0  |  0  |  0  |  0  |  0  |  1  |  0  |  82H   |

如下为 8255 基本输入输出实验接线图

![[微机原理/实验/src/Pasted image 20211212173316.png]]

### 4.2. 程序清单

```nasm
IOY0 		EQU 0600H 		;片选 IOY0 对应的端口始地址
MY8255_A 	EQU IOY0+00H*2 	;8255 的 A 口地址
MY8255_B 	EQU IOY0+01H*2 	;8255 的 B 口地址
MY8255_C 	EQU IOY0+02H*2 	;8255 的 C 口地址
MY8255_MODE EQU IOY0+03H*2 	;8255 的控制寄存器地址
SSTACK 	SEGMENT STACK
		DW 32 DUP(?)
SSTACK 	ENDS
CODE SEGMENT
		ASSUME CS:CODE
START: 	MOV DX, MY8255_MODE
		MOV AL, 82H			;控制字
		OUT DX, AL 			;初始化
AA1: 	MOV DX, MY8255_B
		IN AL, DX
		CALL DELAY
		MOV DX, MY8255_A
		OUT DX, AL
		JMP AA1
DELAY: 	PUSH CX				;延迟
		MOV CX, 0F00H
AA2: 	PUSH AX
		POP AX
		LOOP AA2
		POP CX
		RET
CODE ENDS
		END START
```

### 4.3 实验结果

拨动开关，LED 灯会按照开关的开合显示。

## 5. 实验 5-2 流水灯显示实验

### 5.1. 实验要求

使 8255 的 A 口和 B 口均为输出，数据灯 D7~D0 由左向右，每次仅亮一个灯，循环显示，D15~D8 与 D7~D0 正好相反，由右向左，每次仅点亮一个灯，循环显示。

| D7  | D6  | D5  | D4  | D3  | D2  | D1  | D0  | 控制字 |
|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:------:|
|  1  |  0  |  0  |  0  |  0  |  0  |  0  |  0  |  80H   |


![[微机原理/实验/src/Pasted image 20211212174602.png]]

### 5.2. 程序清单

```nasm
IOY0 		EQU 0600H 		;片选IOY0对应的端口始地址
MY8255_A 	EQU IOY0+00H*2 	;8255的A口地址
MY8255_B 	EQU IOY0+01H*2 	;8255的B口地址
MY8255_C 	EQU IOY0+02H*2 	;8255的C口地址
MY8255_MODE EQU IOY0+03H*2 	;8255的控制寄存器地址
SSTACK 	SEGMENT STACK
		DW 32 DUP(?)
SSTACK ENDS
CODE SEGMENT
		ASSUME CS:CODE
START:
		MOV DX, MY8255_MODE
		MOV AL, 80H
		OUT DX, AL
		;若要改变亮的顺序改变移位顺序，改变初始亮的位置改变初始值
		MOV BL, 80H ;>>
		MOV BH, 01H ;<<
AA1:
		MOV DX, MY8255_A
		MOV AL, BL
		OUT DX, AL
		ROR BL, 1 			;右移一位
		MOV DX, MY8255_B
		MOV AL, BH
		OUT DX, AL
		ROL BH, 1			;左移一位
		;延时
		CALL DELAY
		JMP AA1
DELAY: 	PUSH CX
		MOV CX, 0F000H
AA2:	PUSH AX
		POP AX
		LOOP AA2
		POP CX
		RET
CODE ENDS
		END START
```

### 5.3. 实验结果

A 端口和 B 端口的 LED 灯循环移位，交替显示。

![[微机原理/实验/src/5-2.svg]]

## 6. 实验 5-3 选通输入输出实验

### 6.1. 实验要求

本实验使 8255 端口 A 工作在方式 0 并作为输出口，端口 B 工作在方式 1 并作为输入口，则端口 C 的 PC2 成为选通信号输入端 STBB，PC0 成为中断请求信号输出端 INTRB。当 B 口数据就绪后，通过发 STBB 信号来请求 CPU 读取端口 B 数据并送端口 A 输出显示。用一组开关信号接入端口 B，端口 A 输出线接至一组数据灯上。

| D7  | D6  | D5  | D4  | D3  | D2  | D1  | D0  | 控制字 |
|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:---:|:------:|
|  1  |  0  |  0  |  0  |  0  |  1  |  1  |  0  |  86H   |

![[微机原理/实验/src/Pasted image 20211212175700.png]]

### 6.2. 程序清单

```nasm
IOY0 		EQU 0600H 		;片选IOY0对应的端口始地址
MY8255_A 	EQU IOY0+00H*2 	;8255的A口地址
MY8255_B 	EQU IOY0+01H*2 	;8255的B口地址
MY8255_C 	EQU IOY0+02H*2 	;8255的C口地址
MY8255_MODE EQU IOY0+03H*2 	;8255的控制寄存器地址
STACK1 	SEGMENT STACK
		DW 256 DUP(?)
STACK1 ENDS
CODE SEGMENT
		ASSUME CS:CODE
START: 	MOV DX,MY8255_MODE
		MOV AL,86H
		OUT DX,AL
		MOV DX,MY8255_MODE
		MOV AL,00H			;0XXX 000 0 => PC0 复位
		OUT DX,AL
		MOV AL,05H 			;0XXX 010 1 => PC2 置位
		OUT DX,AL
		PUSH DS
		MOV AX, 0000H
		MOV DS, AX
		MOV AX, OFFSET MIR7
		MOV SI, 003CH
		MOV [SI], AX
		MOV AX, CS
		MOV SI, 003EH
		MOV [SI], AX
		CLI 				;关中断初始化
		POP DS
		;初始化主片8259
		MOV AL, 11H
		OUT 20H, AL    		;ICW1
		MOV AL, 08H
		OUT 21H, AL    		;ICW2
		MOV AL, 04H
		OUT 21H, AL    		;ICW3
		MOV AL, 01H
		OUT 21H, AL    		;ICW4
		MOV AL, 6FH    		;OCW1
		OUT 21H, AL
		STI 				;开
AA1: 	NOP
		JMP AA1
MIR7: 	MOV DX,MY8255_B
		IN AL,DX
		MOV DX,MY8255_A 	;写A口
		OUT DX,AL
		MOV AL,20H
		OUT 20H,AL
		IRET
DELAY: 	PUSH CX
		MOV CX, 0F00H
AA2: 	PUSH AX
		POP AX
		LOOP AA2
		POP CX
		RET
CODE ENDS
		END START
```

### 6.3. 实验结果

拨动开关，再按下 KK1-，LED 灯会按照开关的开与合更新显示。

![[微机原理/实验/src/5-31.svg]]

## 7. 修改代码 实现按键控制流水灯

### 7.1. 流程图

![[微机原理/实验/src/5-4.svg]]

### 7.2. 代码实现

```nasm
IOY0        EQU 0600H       ;片选IOY0对应的端口始地址
MY8255_A    EQU IOY0+00H*2  ;8255的A口地址
MY8255_B    EQU IOY0+01H*2  ;8255的B口地址
MY8255_C    EQU IOY0+02H*2  ;8255的C口地址
MY8255_MODE EQU IOY0+03H*2  ;8255的控制寄存器地址
SSTACK  SEGMENT STACK 
        DW 32 DUP(?) 
SSTACK ENDS 
CODE SEGMENT 
        ASSUME CS:CODE 
START:  
        MOV DX, MY8255_MODE
        MOV AL, 80H
        OUT DX, AL
        ;若要改变亮的顺序改变移位顺序，改变初始亮的位置改变初始值
        MOV BL, 80H
        MOV BH, 01H
        
        MOV DX, MY8255_A
        MOV AL, BL
        OUT DX, AL
        
        MOV DX, MY8255_B
        MOV AL, BH
        OUT DX, AL
        
        PUSH DS 
        MOV AX, 0000H 
        MOV DS, AX 
        MOV AX, OFFSET MIR7 ; 取中断入口地址 
        MOV SI, 003CH       ; 中断矢量地址 
        MOV [SI], AX        ; 填 IRQ7 的偏移矢量 
        MOV AX, CS          ; 段地址
        MOV SI, 003EH 
        MOV [SI], AX        ; 填 IRQ7 的段地址矢量 
        CLI                 ; IF 清零，关中断
        POP DS 
        ;初始化主片 8259 
        MOV AL, 11H 
        OUT 20H, AL         ; ICW1 
        MOV AL, 08H         
        OUT 21H, AL         ; ICW2 
        MOV AL, 04H         
        OUT 21H, AL         ; ICW3 
        MOV AL, 01H         
        OUT 21H, AL         ; ICW4 
        MOV AL, 6FH         ; OCW1 0110 1111 开放 IR4 和 MIR7
        OUT 21H, AL 
        STI                 ; IF 设为 1，开中断
        
        
AA1:    NOP                 ; 空指令循环
        JMP AA1

; 中断服务程序 
MIR7:   STI 
        CALL DELAY          ; 延迟
        MOV AX, 0137H       ; AH=01H, AL=37H 字符 7
        INT 10H             ; 显示字符 7 
        MOV AX, 0120H       ; AH=01H, AL=20H 空格
        INT 10H             ; 显示空格
        
    
        PUSH CX
        MOV CX,0008H
AA3:	MOV DX, MY8255_A
        MOV AL, BL
        OUT DX, AL
        ROR BL, 1           ;右移一位
        MOV DX, MY8255_B
        MOV AL, BH
        OUT DX, AL
        ROL BH, 1           ;左移一位
        CALL DELAY1
        LOOP AA3
        POP CX
        
        MOV AL, 20H
        OUT 20H, AL         ; 中断结束命令 
        IRET                ; 返回
        
DELAY:  PUSH CX             ; 延迟一段时间
        MOV CX, 0F00H 
AA0:    PUSH AX 
        POP AX 
        LOOP AA0 
        POP CX 
        RET 
        
DELAY1:  PUSH CX             ; 延迟一段时间
        MOV CX, 0F000H 
AA4:    PUSH AX 
        POP AX 
        LOOP AA4
        POP CX 
        RET 
CODE ENDS 
        END START
```

### 7.3. 实验结果

![[微机原理/实验/src/Pasted image 20211228094328.png]]

