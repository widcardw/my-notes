---
date created: 2021-12-01 22:58
---

# 实验四 8253 定时器计数器

## 1. 实验目的

1. 掌握 8254 的工作方式及应用编程。
2. 掌握 8254 典型应用电路的接法。

## 2. 实验内容

1. 计数应用实验。编写程序，应用 8254 的计数功能，使用单次脉冲模拟计数，使每当按动 `KK1+` 5 次后，产生一次计数中断，并在屏幕上显示一个字符 `M`。
2. 定时应用实验。编写程序，应用 8254 的定时功能，产生一个 1s 的方波，并用本装置的示波器功能来观察。

## 3. 实验原理

8254 实验单元电路图

![[微机原理/实验/src/Pasted image 20211127201706.png]]

## 4. 实验 4-1 计数应用实验

将 8254 的计数器 0 设置为方式 0，计数值为十进制数 4，用单次脉冲 `KK1+` 作为 `CLK0` 时钟，`OUT0` 连接 `MIR7`，每当 `KK1+` 按动 5 次后产生中断请求， 在屏幕上显示字符 `M`。

> 8253在 $\overline{WR}$ 信号的上升沿将计数值写入 `CR`，并在下一个 `CLK` 脉冲开始计数，即如果计数初值为 $N$，在 $N+1$ 个 `CLK` 脉冲之后输出信号 `OUT` 变为高电平。因此，在这里写入初值为 4，最终会计数 5 次。

![[微机原理/实验/src/Pasted image 20211127201846.png]]

![[微机原理/实验/src/Pasted image 20211128211437.png]]

### 4.1. 实验步骤

1. 实验接线如图 4.1 所示（由于 8254 单元中 `GATE0` 信号已经上拉 +5V，所以 `GATE0` 不用接线）
2. 编写实验程序（例程文件名为：`A82541.ASM`），经编译、链接无误后装入系统
3. 单击 `RUN` 按钮，运行实验程序，每连续按动 5 次 `KK1+`，在界面的输出区会显示字符 `M`，观察实验现象
4. 改变计数值，验证 8254 的计数功能

### 4.2. 实验程序清单

```cpp
;========================================================= 
; 文件名: A82541.ASM 
; 功能描述: 通过对计数器 0 进行计数，计数初值为 4， 
; 当计数满后，产生正跳变触发中断，中断 
; 程序显示 M (每按 5 次输出一个 M) 
;========================================================= 
IOY0 	EQU 0600H 				; IOY0 起始地址 
A8254 	EQU IOY0+00H*2 
B8254 	EQU IOY0+01H*2 
C8254 	EQU IOY0+02H*2 
CON8254	EQU IOY0+03H*2 

SSTACK 	SEGMENT STACK 
		DW 32 DUP(?)
SSTACK 	ENDS

CODE 	SEGMENT
		ASSUME CS:CODE,SS:SSTACK
START:	PUSH DS
		MOV AX, 0000H 
		MOV DS, AX 
		MOV AX, OFFSET IRQ7 	; 取中断入口地址 
		MOV SI, 003CH 			; 中断矢量地址 
		MOV [SI], AX 			; 填 IRQ7 的偏移矢量 
		MOV AX, CS 				; 段地址 
		MOV SI, 003EH 
		MOV [SI], AX 			; 填 IRQ7 的段地址矢量 
		CLI 
		POP DS 
		; 初始化主片 8259 
		MOV AL, 11H 
		OUT 20H, AL 			; ICW1 
		MOV AL, 08H 
		OUT 21H, AL 			; ICW2 
		MOV AL, 04H 
		OUT 21H, AL 			; ICW3 
		MOV AL, 01H 
		OUT 21H, AL 			; ICW4 
		MOV AL, 6FH 			; OCW1 
		OUT 21H, AL
		; 8254 
		MOV DX, CON8254 
		MOV AL, 10H 			; 00 01 000 0，计数器 0，方式 0
		OUT DX, AL 
		MOV DX, A8254 
		MOV AL, 04H 			; 值为十进制 4
		OUT DX, AL 
		STI 
AA1: 	JMP AA1 
IRQ7: 	MOV DX, A8254 
		MOV AL, 04H 
		OUT DX, AL 
		MOV AX, 014DH 
		INT 10H 				; 显示字符 M 
		MOV AX, 0120H 
		INT 10H 
		MOV AL, 20H 
		OUT 20H, AL 			; 中断结束命令
		IRET 
CODE 	ENDS 
		END START
```

### 4.3. 实验结果

![[微机原理/实验/src/T4_1.png]]

## 5. 实验 4-2 定时应用实验

将 8254 的计数器 0 和计数器 1 都设置为方式 3，用信号源 1MHz 作为 `CLK0` 时钟，`OUT0` 为波形输出 1ms 方波，再通过 `CLK1` 输入，`OUT1` 输出 1s 方波。

![[微机原理/实验/src/Pasted image 20211127203022.png]]

### 5.1. 实验步骤

1. 接线图如图 4.4 所示
2. 根据实验内容，编写实验程序（例程文件名为：`A82542.ASM`），经编译、链接无误后装入系统
3. 单击 `RUN` 按钮，运行实验程序，8254 的 `OUT1` 会输出 1s 的方波，可用软件自带的示波器功能进行观察
4. 用示波器观察波形的方法：单击虚拟仪器菜单中的 `示波器` 按钮或直接单击工具栏的 `☡` 按钮，在新弹出的示波器界面上单击 `⚡` 按钮运行示波器， 就可以观测出 `OUT1` 输出的波形。

### 5.2. 实验程序清单

```cpp
;定义常量
IOY0 	EQU 0600H          ;IOY0 起始地址
A8254 	EQU IOY0+00H*2
B8254 	EQU IOY0+01H*2
C8254 	EQU IOY0+02H*2
CON8254	EQU IOY0+03H*2

SSTACK SEGMENT STACK8259 
SSTACK ENDS

CODES SEGMENT
		ASSUME CS:CODES, SS:SSTACK
START:	
		MOV DX, CON8254	;8254
		MOV AL, 37H		;00 11 011 1   计数器 0，先读写低 8 位再读写高 8 位，方式 3
		OUT DX, AL      ;设置好的数写入引脚地址

		MOV DX, A8254  
		MOV AL, 00H   
		OUT DX, AL     
		MOV AL, 10H
		OUT DX, AL      
		
		MOV DX, CON8254	;8254
		MOV AL, 77H	;01 11 011 1  计数器 1，方式 3，BCD 输入
		OUT DX, AL

		MOV DX, B8254
		MOV AL, 00H 
		OUT DX, AL
		MOV AL, 10H
		OUT DX, AL
		
AA1:	JMP AA1
CODES	ENDS
		END  START
```

### 5.3. 实验结果

计数器 0 和计数器 1 串联，两个计数器计数值均为 1000，经过两次分频后 1 MHz 的时钟频率在 OUT1 端输出为 1 Hz，形成方波。

由于实验硬件问题，未能看到明显的现象。

## 6. 实验 4-3 电子发声实验

### 6.1. 实验介绍

一个音符对应一个频率，将对应一个音符频率的方波通到扬声器上，就可以发出这个音符的声音。将一段乐曲的音符对应频率的方波依次送到扬声器，就可以演奏出这段乐曲。利用 8254 的方式 3——“方波发生器”，将相应一种频率的计数初值写入计数器，就可产生对应频率的方波。计数初值的计算如下：

$$
\text{计数初值}=\text{输入时钟}\div \text{输出频率}
$$

例如输入时钟采用 1MHz，要得到 800Hz 的频率，计数初值即为 1000000÷800。对于每一个音符的演奏时间，可以通过软件延时来处理。首先确定单位延时时间程序（根据 CPU 的频率不同而有所变化）。然后确定每个音符演奏需要几个单位时间，将这个值送入 `DL` 中，调用 `DALLY` 子程序即可。

```cpp
; 单位延时时间，循环 F0H 次
DALLY PROC 
D0: MOV CX, 0010H 
D1: MOV AX, 0F00H 
D2: DEC AX 
	JNZ D2 
	LOOP D1 
	RET 
DALLY ENDP 
; N 个单位延时时间 (N 送至 DL)，循环 N * F0H 次
DALLY PROC 
D0: MOV CX, 0010H 
D1: MOV AX, 0F00H 
D2: DEC AX 
	JNZ D2 
	LOOP D1 
	DEC DL 
	JNZ D0 
	RET 
DALLY ENDP
```

![[微机原理/实验/src/Pasted image 20211127210432.png]]

频率表和时间表是一一对应的，频率表的最后一项为 0，作为重复的标志。根据频率表中的频率算出对应的计数初值，然后依次写入 8254 的计数器。将时间表中相对时间值带入延时程序来得到音符演奏时间。

![[微机原理/实验/src/Pasted image 20211127210514.png]]

### 6.2. 实验步骤

1. 参考图 4.8 所示连接实验线路。
2. 编写实验程序，经编译、连接无误后装入系统。
3. 运行程序，听扬声器发出的音乐是否正确。
4. 固化程序，然后脱机运行程序。

![[微机原理/实验/src/Pasted image 20211127210623.png]]

### 6.3. 实验程序清单

```cpp
;================================================================
; SOUND.asm
; 电子发声设计实验
;================================================================
; 端口定义 
IOY0 EQU 0600H 
MY8254_COUNT0 EQU IOY0+00H*2 	;8254 计数器 0 端口地址 
MY8254_COUNT1 EQU IOY0+01H*2 	;8254 计数器 1 端口地址 
MY8254_COUNT2 EQU IOY0+02H*2 	;8254 计数器 2 端口地址 
MY8254_MODE EQU IOY0+03H*2 		;8254 控制寄存器端口地址
STACK1 SEGMENT STACK 
		DW 256 DUP(?) 
STACK1 ENDS
DATA SEGMENT 
FREQ_LIST 	DW 371,495,495,495,624,556,495,556,624 		;频率表 
			DW 495,495,624,742,833,833,833,742,624 
			DW 624,495,556,495,556,624,495,416,416,371 
			DW 495,833,742,624,624,495,556,495,556,833 
			DW 742,624,624,742,833,990,742,624,624,495 
			DW 556,495,556,624,495,416,416,371,495,0
TIME_LIST 	DB 4, 6, 2, 4, 4, 6, 2, 4, 4 				;时间表 
			DB 6, 2, 4, 4, 12, 1, 3, 6, 2 
			DB 4, 4, 6, 2, 4, 4, 6, 2, 4, 4 
			DB 12, 4, 6, 2, 4, 4, 6, 2, 4, 4 
			DB 6, 2, 4, 4, 12, 4, 6, 2, 4, 4 
			DB 6, 2, 4, 4, 6, 2, 4, 4, 12 
DATA ENDS
CODE SEGMENT 
		ASSUME CS:CODE, DS:DATA 
START: 	MOV AX, DATA 
		MOV DS, AX 
		MOV DX, MY8254_MODE 	;初始化 8254 工作方式 
		MOV AL, 36H 			;定时器 0、方式 3 
		OUT DX, AL 
BEGIN: 	MOV SI,OFFSET FREQ_LIST ;装入频率表起始地址 
		MOV DI,OFFSET TIME_LIST ;装入时间表起始地址 
PLAY: 	MOV DX,0FH 		;输入时钟为 1MHz，1M = 0F4240H 
		MOV AX,4240H 
		DIV WORD PTR [SI] ;取出频率值计算计数初值，0F4240H / 输出频率
        ; 除法 DX:AX / WORD PTR [SI]，结果放入 AX
		MOV DX,MY8254_COUNT0 
		OUT DX,AL   ;装入计数初值，先写低位
		MOV AL,AH 
		OUT DX,AL   ;再写高位
		MOV DL,[DI] ;取出演奏相对时间，调用延时子程序 
		CALL DALLY 
		ADD SI,2 
		INC DI 
		CMP WORD PTR [SI],0 ;判断是否到曲末
		JE BEGIN 
		JMP PLAY
DALLY PROC ; 延时子程序 
D0: 	MOV CX,0010H 
D1: 	MOV AX,0F00H 
D2: 	DEC AX 
		JNZ D2 
		LOOP D1 
		DEC DL 
		JNZ D0 
		RET 
DALLY ENDP 
CODE ENDS 
		END START
```

