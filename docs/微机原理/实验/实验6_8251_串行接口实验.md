---
date created: 2021-12-20
---

# 实验六 8251 串行接口实验

## 1. 实验目的

1. 掌握 8251 的工作方式及应用。
2. 了解有关串口通讯的知识。

## 2. 实验内容

1. 数据信号的串行传输实验，循环向串口发送一个数，使用示波器测量 TXD 引脚上的波形，以了解串行传输的数据格式。
2. 自收自发实验，将 3000H 起始的 10 个单元中的初始数据发送到串口，然后自接收并保存到 4000H 起始的内存单元中。
3. 双机通讯实验，本实验需要两台实验装置，其中一台作为接收机，一台作为发送机，发送机将 3000H~3009H 内存单元中共 10 个数发送到接收机，接收机将接收到的数据直接在屏幕上输出显示。

## 3. 8251 单元电路图

![[微机原理/实验/src/Pasted image 20211219121923.png]]

## 4. 实验 6-1 数据信号的串行传输

### 4.1. 实验步骤

发送往串口的数据会以串行格式从 TXD 引脚输出，编写程序，观察串行输出的格式。

1. 按图 6.2 连接实验接线
2. 编写实验程序，经编译、链接无误后装入系统。
3. 单击 `Run` 按钮，运行实验程序，TXD 引脚上输出串行格式的数据波形。
4. 用示波器观察波形的方法：单击虚拟仪器菜单中的 `示波器` 按钮，在新弹出的示波器界面上单击 `⚡` 按钮运行示波器，观测实验波形，分析串行数据传输格式。

![[微机原理/实验/src/Pasted image 20211219122646.png]]

### 4.2. 实验程序

```nasm
IOY0 		EQU 0600H       ;IOY0 起始地址
IOY1 		EQU 0640H       ;IOY1 起始地址
M8251_DATA	EQU IOY0+00H*2
M8251_CON 	EQU IOY0+01H*2  ;8251 控制口
M8254_2 	EQU IOY1+02H*2
M8254_CON 	EQU IOY1+03H*2  ;8254 控制口
SSTACK SEGMENT STACK
	DW 64 DUP(?)
SSTACK ENDS
CODE SEGMENT
	ASSUME CS:CODE
START: 
    CALL INIT
A1: CALL SEND
	MOV CX, 0001H
A2: MOV AX, 0F00H
A3: DEC AX
	JNZ A3
	LOOP A2
	JMP A1
INIT: 
    MOV AL, 0B6H        ; 8254, 设置通讯时钟 10 11 011 0
	MOV DX, M8254_CON   ; 通道2, 16位, 方式3(方波), 二进制
	OUT DX, AL
	MOV AL, 1BH         
	MOV DX, M8254_2
	OUT DX, AL
	MOV AL, 3AH         ; 计数值为 3A1BH=14875
	OUT DX, AL
	CALL RESET          ; 对 8251 进行初始化
	CALL DALLY
	MOV AL, 7EH ; 01 11 11 10 (一个停止位, 偶校验, 8位长度, 16位异步)
	MOV DX, M8251_CON   ; 写 8251 方式字
	OUT DX, AL
	CALL DALLY
	MOV AL, 34H         ; 0011 0100, 请求发送, 出错复位, 允许接收
	OUT DX, AL          ; 写 8251 控制字
	CALL DALLY
	RET
RESET:
    MOV AL, 00H         ; 初始化 8251 子程序
	MOV DX, M8251_CON   ; 控制寄存器
	OUT DX, AL
	CALL DALLY
	OUT DX, AL
	CALL DALLY
	OUT DX, AL          ; 三次重置, 确认不是误操作
	CALL DALLY
	MOV AL, 40H         ; 0100 0000 命令指令字 IR=1, 内部复位
	OUT DX, AL
	RET
DALLY: 
    PUSH CX
	MOV CX, 5000H
A4: PUSH AX
	POP AX
	LOOP A4
	POP CX
	RET
SEND: 
    PUSH AX
	PUSH DX
	MOV AL, 31H         ; 0011 0001, 命令指令字
	MOV DX, M8251_CON   ; 请求发送, 出错标志复位, 允许发送
	OUT DX, AL
	MOV AL, 55H
	MOV DX, M8251_DATA  ; 发送数据 55H
	OUT DX, AL
	POP DX
	POP AX
	RET
CODE ENDS
	END START
```

![[微机原理/实验/src/6-1.svg]]

### 4.3. 实验结果

![[微机原理/实验/src/Pasted image 20211228114309.png]]

## 5. 实验 6-2 自收自发实验

### 5.1. 实验步骤

通过自收自发实验，可以验证硬件及软件设计，常用于自测试（采用查询方式实现）。

1. 参考实验接线图如图 6.3 所示，按图连接实验线路。
2. 编写实验程序，编译、链接无误后装入系统。
3. 使用 E 命令更改 4000H 起始的 10 个单元中的数据。
4. 运行实验程序，待程序运行停止。
5. 查看 3000H 起始的 10 个单元中的数据，与初始化的数据进行比较，验证程序功能。

![[微机原理/实验/src/Pasted image 20211219123303.png]]

### 5.2. 实验代码

```nasm
;===========================================================
; 文件名: A82512.ASM
; 功能描述: 自收自发实验程序, 源地址4000H, 目的地址3000H
;===========================================================
IOY0         	EQU  0600H        ;IOY0起始地址
IOY1         	EQU  0640H        ;IOY1起始地址
M8251_DATA		EQU IOY0+00H*2
M8251_CON		EQU IOY0+01H*2
M8254_2			EQU IOY1+02H*2
M8254_CON		EQU IOY1+03H*2

SSTACK	SEGMENT STACK
		DW 64 DUP(?)
SSTACK	ENDS
CODE	SEGMENT
		ASSUME CS:CODE
START:	MOV AX, 0000H
		MOV DS, AX
		;初始化8254，得到收发时钟
		MOV AL, 0B6H        ; 1011 011 0
		MOV DX, M8254_CON   ; 通道2, 16位, 方式3, 二进制
		OUT DX, AL
		MOV AL, 0CH
		MOV DX, M8254_2
		OUT DX, AL
		MOV AL, 00H
		OUT DX, AL          ; 计数值为 000CH=12
		;复位8251
		CALL INIT
		CALL DALLY
		;8251方式字
		MOV AL,7EH ; 01 11 11 10 (一个停止位, 偶校验, 8位长度, 16位异步)
		MOV DX, M8251_CON 
		OUT DX, AL
		CALL DALLY
		;8251控制字 
		MOV AL, 34H         ; 0011 0100, 请求发送, 出错复位, 允许接收
		OUT DX, AL
		CALL DALLY
		MOV DI, 3000H
		MOV SI, 4000H
		MOV CX, 000AH       ; 0AH=10
A1:		MOV AL, [SI]
		PUSH AX
		MOV AL, 37H         ; 0011 0111, 请求发送, 出错复位
		MOV DX, M8251_CON   ; 允许接收, 数据端就绪, 允许发送
		OUT DX, AL 
		POP AX			    ; AX == [SI]
		MOV DX, M8251_DATA
		OUT DX, AL			; 发送数据
		MOV DX, M8251_CON 
A2:		IN AL, DX			; 判断发送缓冲是否为空
		AND AL, 01H
		JZ A2
		CALL DALLY
A3:		IN AL, DX			; 判断是否接收到数据
		AND AL, 02H
		JZ A3
		MOV DX, M8251_DATA
		IN AL, DX			; 读取接收到的数据
		MOV [DI], AL        ; 传送到 [DI]
		INC DI              ; 指针后移
		INC SI
		LOOP A1
		MOV AX,4C00H
		INT 21H				; 程序终止
INIT:	MOV AL, 00H			; 复位8251子程序
		MOV DX, M8251_CON
		OUT DX, AL
		CALL DALLY
		OUT DX, AL
		CALL DALLY
		OUT DX, AL
		CALL DALLY
		MOV AL, 40H         ; 复位命令字
		OUT DX, AL          
		RET
DALLY:	PUSH CX
		MOV CX,3000H
A5:		PUSH AX
		POP AX
		LOOP A5
		POP CX
		RET		
CODE	ENDS
		END START
```

### 5.3. 实验结果

![[微机原理/实验/src/Pasted image 20211228100322.png]]

## 6. 实验 6-3 双机通讯实验

### 6.1. 实验步骤

使用两台实验装置，一台为发送机，一台为接收机，进行两机间的串行通讯。

1. 按图 6.4 连接实验线路。
2. 为两台机器分别编写实验程序（发送和接收），编译、链接后装入系统。
3. 为发送机初始化发送数据。在发送机 3000H~3009H 内存单元写入 ASCII 值：30~39  共 10 个数。
4. 首先运行接收机上的程序，等待接收数据，然后运行发送机上的程序，将数据发送到串口。
5. 观察接收机端屏幕上的显示是否与发送机端初始的数据相同，验证程序功能。

![[微机原理/实验/src/Pasted image 20211219123512.png]]

### 6.2. 实验程序代码

OUT2 频率为 153.6 KHz，已经连到 RxCLK 和 TxCLK 上。 异步通信时，是用外部时钟来和接收的数据进行同步的，8251 需要时钟来控制其波特率，以控制传输速率。如下计算得到计数值为 12，其中 9600 为传输波特率，16 为异步传输的位数。

$$
1.8432\,\mathrm{MHz}\div 9600\div 16=12\,\mathrm{Hz}
$$

```nasm
;=========================================================
; 文件名: A82514.ASM
; 功能描述: 发送机的发送程序
;=========================================================

IOY0        EQU  0600H        ;IOY0起始地址
IOY1        EQU  0640H        ;IOY1起始地址
M8251_DATA	EQU IOY0+00H*2
M8251_CON	EQU IOY0+01H*2
M8254_2		EQU IOY1+02H*2
M8254_CON	EQU IOY1+03H*2

SSTACK	SEGMENT STACK
		DW 64 DUP(?)
SSTACK	ENDS
CODE	SEGMENT
		ASSUME CS:CODE
START:	MOV AL, 0B6H			;初始化8254, 得到收发时钟
		MOV DX, M8254_CON       ;通道2, 16位, 方式3, 二进制
		OUT DX, AL
		MOV AL, 0CH
		MOV DX, M8254_2
		OUT DX, AL
		MOV AL, 00H             ;写入计数值12
		OUT DX, AL              
		CALL INIT				;复位8251
		CALL DALLY
		MOV AL, 7EH             ;01 11 11 10 
		MOV DX, M8251_CON       ;一个停止位, 偶校验, 8位长度, 16位异步
		OUT DX, AL				;8251方式字
		CALL DALLY
		MOV AL, 34H             ;0011 0100, 请求发送, 出错复位, 允许接收
		OUT DX, AL				;8251控制字
		CALL DALLY
		MOV DI, 3000H
		MOV CX, 000AH
A1:		MOV AL, [DI]
		CALL SEND               ;发送
		CALL DALLY
		INC DI
		LOOP A1
A2:		JMP A2
INIT:	MOV AL, 00H				;复位8251子程序
		MOV DX, M8251_CON
		OUT DX, AL
		CALL DALLY
		OUT DX, AL
		CALL DALLY
		OUT DX, AL
		CALL DALLY
		MOV AL, 40H             ; 复位命令字
		OUT DX, AL
		RET
DALLY:	PUSH CX
		MOV CX, 3000H
A4:		PUSH AX
		POP AX
		LOOP A4
		POP CX
		RET
SEND:	PUSH DX
		PUSH AX                 ; AX 中放了目的地址的数据
		MOV AL, 31H             ; 请求发送, 出错标志复位, 允许发送
		MOV DX, M8251_CON
		OUT DX, AL
		POP AX                  ; AX 中放待传数据
		MOV DX, M8251_DATA
		OUT DX, AL
		MOV DX, M8251_CON
A3:		IN AL, DX               ; 检测发送缓冲区是否为空
		AND AL, 01H
		JZ A3
		POP DX
		RET
CODE	ENDS
		END START
```

```nasm
;========================================================
; 文件名: A82513.ASM
; 功能描述: 接收机接收程序
;========================================================

IOY0        EQU  0600H        ;IOY0起始地址
IOY1        EQU  0640H        ;IOY1起始地址
M8251_DATA	EQU IOY0+00H*2
M8251_CON	EQU IOY0+01H*2
M8254_2		EQU IOY1+02H*2
M8254_CON	EQU IOY1+03H*2

SSTACK	SEGMENT STACK
		DW 64 DUP(?)
SSTACK	ENDS
CODE	SEGMENT
		ASSUME CS:CODE
START:	MOV AL, 0B6H			;初始化8254
		MOV DX, M8254_CON
		OUT DX, AL
		MOV AL, 0CH
		MOV DX, M8254_2
		OUT DX, AL
		MOV AL, 00H
		OUT DX, AL
	                        	;CLI
		CALL INIT				;复位8251
		CALL DALLY
		MOV AL, 7EH             ;写8251方式字
		MOV DX, M8251_CON ; 01 11 11 10 (一个停止位, 偶校验, 8位长度, 16位异步)
		OUT DX, AL 
		CALL DALLY
		MOV AL, 34H         ; 0011 0100, 请求发送, 出错复位, 允许接收
		OUT DX, AL          ;控制字
		CALL DALLY
		MOV AX, 0152H		;输出显示字符R
		INT 10H
		MOV DI, 3000H       ;目的地址
		MOV CX, 000AH       ;循环次数
A1:		MOV DX,M8251_CON
        IN AL, DX
		AND AL, 02H         ;接收缓冲区是否为空
		JZ A1
		MOV DX, M8251_DATA
		IN AL, DX           ;读取 ASCII 码
		AND AL, 7FH         ;去除最高位
		MOV [DI],AL         ;写入存储器
		INC DI
		LOOP A1
		MOV AL, 00H
		MOV SI, 300AH       ;在 300AH 处写一个字节 00H
		MOV [SI], AL
		MOV AH, 06H         ;调用系统中断AH=06H
		MOV BX, 3000H       ;从 BX 所指的地址开始显示，直至遇到值为 00H 的字节
		INT 10H				;输出显示接收到的数据
		                    ;STI
A2:		JMP A2
INIT:	MOV AL, 00H			;复位8251子程序
		MOV DX, M8251_CON
		OUT DX, AL
		CALL DALLY
		OUT DX, AL
		CALL DALLY
		OUT DX, AL          ; 3 次置 0 复位
		CALL DALLY
		MOV AL, 40H         ; 复位命令字
		OUT DX, AL
		RET
DALLY:	PUSH CX
		MOV CX, 3000H
A3:		PUSH AX
		POP AX
		LOOP A3
		POP CX
		RET
CODE	ENDS
		END START
```

### 6.3. 实验结果

接收机收到发送机发送的十个字符 `ABCDEFGHIJ`

![[微机原理/实验/src/Pasted image 20211228100449.png]]

